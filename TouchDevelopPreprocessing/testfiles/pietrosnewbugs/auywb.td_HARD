meta version "v2.2,nothing";
meta name "bullsNcows";
meta icon "questioncirclealt";
meta color "#ff9955bb";
// Игра быки и коровы
meta platform "current";

action main() {
  $task := code→makeTask;
  $guess := "";
  $taskGuessed := false;
  $attempts := 0;
  while not $taskGuessed do {
    $guess := wall→ask_string("Ваше число?");
    $taskGuessed := code→makeBullsCows($task, $guess);
    $attempts := $attempts + 1;
  }
  ("Вы угадали! " ∥ $attempts ∥ " попыток")→post_to_wall;
}

action makeTask() returns ntask: String {
  $digits1 := "0123456789";
  $ntask := "";
  for 0 ≤ i < 4 do {
    $j := math→random($digits1→count);
    $ntask := $ntask ∥ $digits1→at($j);
    $digits1 := $digits1→split($digits1→at($j))→join("");
  }
  meta private;
}

action makeBullsCows(task: String, guess: String) returns isGuessed: Boolean {
  $bulls := 0;
  $cows := 0;
  if $guess→is_match_regex("^\\d{4}$") then {
    for 0 ≤ i < $guess→count do {
      $c := $guess→at($i);
      if $c→equals($task→at($i)) then {
        $bulls := $bulls + 1;
      }
      else {
        if $task→contains($c) then {
          $cows := $cows + 1;
        }
      }
    }
    ($bulls ∥ " " ∥ $cows)→post_to_wall;
    $isGuessed := $bulls = 4;
  }
  else {
    "Плохое число. Хочу 4 разных цифры." →post_to_wall;
    $isGuessed := false;
  }
  meta private;
}
var digits : Sprite_Set {
  // for input output with no keyboard
}

